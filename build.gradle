buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
      classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
      classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.0'
      classpath 'org.kordamp.gradle:stats-gradle-plugin:0.1.1'
  }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'org.kordamp.gradle.stats'
apply from: 'gradle/publish.gradle'

defaultTasks 'build'

group = 'com.vtence.molecule'
version = '0.10-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def libs = [
        simple            : 'org.simpleframework:simple-http:6.0.1',
        jmustache         : 'com.samskivert:jmustache:1.9',
        hamcrest_all      : 'org.hamcrest:hamcrest-all:1.3',
        junit             : 'junit:junit:4.11@jar',
        jmock             : 'org.jmock:jmock:2.6.0@jar',
        jmock_junit4      : 'org.jmock:jmock-junit4:2.6.0@jar',
        juniversal_chardet: 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3',
]

repositories {
  mavenCentral()
}

dependencies {
  compile libs.simple, optional
  compile libs.jmustache, optional

  testCompile libs.hamcrest_all
  testCompile libs.junit
  testCompile libs.jmock
  testCompile libs.jmock_junit4
  testCompile libs.juniversal_chardet
}

task 'version' << {
  println version
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task coverage(dependsOn: ['test', 'jacocoTestReport'])
